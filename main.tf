variable "name" {
  type        = string
  description = "Name used for most resources"
  default     = "k8tre-dev"
}

variable "region" {
  type        = string
  description = "AWS region"
  default     = "eu-west-2"
}

variable "vpc_cidr" {
  type        = string
  description = "VPC CIDR to create"
  default     = "10.0.0.0/16"
}

variable "public_subnets" {
  type        = list(string)
  description = "Public subnet CIDRs to create"
  default = [
    "10.0.1.0/24", "10.0.2.0/24",
  ]
}

variable "private_subnets" {
  type        = list(string)
  description = "Private subnet CIDRs to create. These IPs are used by EKS pods so make it large!"
  default = [
    "10.0.64.0/18", "10.0.128.0/18",
  ]
}

variable "allowed_cidrs" {
  type        = list(string)
  description = "CIDRs allowed to access K8TRE ('myip' is dynamically replaced by your current IP)"
  default     = ["myip"]
}

variable "additional_admin_principals" {
  type        = map(string)
  description = "Additional EKS admin principals"
  default     = {}
}

variable "efs_token" {
  type        = string
  description = "EFS name creation token, if null default to var.name"
  default     = null
}

variable "enable_github_oidc" {
  type        = bool
  description = "Create GitHub OIDC role"
  default     = false
}

variable "deployment_stage" {
  type        = number
  default     = 1
  description = <<EOT
  Multi-stage deployment step.
  This is necessary because Terraform needs to resolve some resources before
  running, but those resource amy not exist yet.
  For the first deployment you must step through these starting at
  '-var deployment_stage=0', then '-var deployment_stage=1'.
  Future deployment can use the highest number (default).
  EOT
  validation {
    condition     = var.deployment_stage >= 0 && var.deployment_stage <= 3
    error_message = "deployment_stage must be one of [0, 1, 2, 3]"
  }
}

# Get IP of caller to optionally limit inbound connections
data "http" "myip" {
  url = "https://checkip.amazonaws.com/"
}

locals {
  allow_ips = [
    for ip in var.allowed_cidrs :
    replace(ip, "/^myip$/", "${chomp(data.http.myip.response_body)}/32")
  ]
}


######################################################################
# VPC
######################################################################

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "6.6.0"

  name = var.name
  cidr = var.vpc_cidr
  # EKS requires at least two AZ (though node groups can be placed in just one)
  azs                = ["${var.region}a", "${var.region}b"]
  public_subnets     = var.public_subnets
  private_subnets    = var.private_subnets
  enable_nat_gateway = true
  single_nat_gateway = true

  # tags = {
  #   "kubernetes.io/cluster/${var.cluster_name}" = "shared"
  # }

  # https://repost.aws/knowledge-center/eks-load-balancer-controller-subnets
  public_subnet_tags = {
    # "kubernetes.io/cluster/${var.cluster_name}" = "shared"
    "kubernetes.io/role/elb" = "1"
  }

  private_subnet_tags = {}
}

# Security group that allows clusters to access each other
resource "aws_security_group" "internal_cluster_access" {
  name_prefix = "internal-cluster-endpoint"
  vpc_id      = module.vpc.vpc_id
  description = "Internal cluster endpoint"

  // allows traffic from the SG itself
  ingress {
    from_port = 0
    to_port   = 0
    protocol  = "-1"
    self      = true
  }
}

# This is not used in any Terraform resource, but can be referenced in
# non-terraform resources e.g. load-balancers
resource "aws_ec2_managed_prefix_list" "service_access_cidrs" {
  name           = "${var.name}-service-access-cidrs"
  address_family = "IPv4"
  max_entries    = 20

  dynamic "entry" {
    for_each = local.allow_ips
    content {
      cidr = entry.value
      # description =
    }
  }
}


######################################################################
# Main K8TRE Kubernetes
######################################################################

locals {
  allow_argocd_k8s_access = {
    description              = "Allow ArgoCD to access internal K8S endpoint"
    type                     = "ingress"
    from_port                = 443
    to_port                  = 443
    protocol                 = "tcp"
    source_security_group_id = aws_security_group.internal_cluster_access.id
  }
}

module "k8tre-eks" {
  source = "./k8tre-eks"
  # source = "git::https://github.com/k8tre/k8tre-infrastructure-aws.git?ref=main"

  deployment_stage = var.deployment_stage

  cluster_name    = var.name
  vpc_id          = module.vpc.vpc_id
  private_subnets = module.vpc.private_subnets

  # k8s_version =

  # CIDRs that have access to the K8S API, e.g. `0.0.0.0/0`
  k8s_api_cidrs = local.allow_ips

  additional_security_groups = [aws_security_group.internal_cluster_access.id]

  # Allow ArgoCD to access K8S API
  cluster_security_group_additional_rules = {
    allow_argocd_k8s_access = local.allow_argocd_k8s_access
  }

  # number_azs        = 1
  # instance_type_wg1 = "t3a.2xlarge"
  # use_bottlerocket  = false
  root_volume_size = 200
  wg1_size         = 2
  wg1_max_size     = 2

  # For available addons see
  # https://docs.aws.amazon.com/eks/latest/userguide/workloads-add-ons-available-eks.html
  # additional_eks_addons = {}

  # autoupdate_ami = false
  # autoupdate_addons = false

  create_pod_identities = true
  hosted_zone_ids = concat(
    [module.dnsresolver.private-zone-id],
    module.dnsresolver.public-zone-id
  )

  github_oidc_rolename = var.enable_github_oidc ? "${var.name}-github-oidc" : null

  additional_admin_principals = var.additional_admin_principals
}


######################################################################
# ArgoCD K8TRE Kubernetes
######################################################################

module "k8tre-argocd-eks" {
  source = "./k8tre-eks"
  # source = "git::https://github.com/k8tre/k8tre-infrastructure-aws.git?ref=main"

  deployment_stage = var.deployment_stage

  cluster_name    = "${var.name}-argocd"
  vpc_id          = module.vpc.vpc_id
  private_subnets = module.vpc.private_subnets

  # k8s_version =

  # CIDRs that have access to the K8S API, e.g. `0.0.0.0/0`
  k8s_api_cidrs = local.allow_ips

  additional_security_groups = [aws_security_group.internal_cluster_access.id]

  # number_azs        = 1
  instance_type_wg1 = "t3a.xlarge"
  # use_bottlerocket  = false
  # root_volume_size = 100
  wg1_size     = 1
  wg1_max_size = 1

  # autoupdate_ami = false
  # autoupdate_addons = false
  create_pod_identities = false

  argocd_create_role            = true
  argocd_assume_eks_access_role = module.k8tre-eks.eks_access_role

  additional_admin_principals = var.additional_admin_principals
}


output "kubeconfig_command_k8tre-dev" {
  description = "Create kubeconfig for k8tre-dev"
  value       = "aws eks update-kubeconfig --name ${module.k8tre-eks.cluster_name}"
}

output "kubeconfig_command_k8tre-argocd-dev" {
  description = "Create kubeconfig for k8tre-argocd-dev"
  value       = "aws eks update-kubeconfig --name ${module.k8tre-argocd-eks.cluster_name}"
}

output "name" {
  description = "Name used for most resources"
  value       = var.name
}

output "efs_token" {
  description = "EFS name creation token"
  value       = local.efs_token
}

output "service_access_prefix_list" {
  description = "ID of the prefix list that can access services running on K8s"
  value       = aws_ec2_managed_prefix_list.service_access_cidrs.id
}

output "vpc_cidr" {
  description = "VPC CIDR"
  value       = module.vpc.vpc_cidr_block
}

output "k8tre_cluster_name" {
  description = "K8TRE dev cluster name"
  value       = module.k8tre-eks.cluster_name
}

output "k8tre_argocd_cluster_name" {
  description = "K8TRE dev cluster name"
  value       = module.k8tre-argocd-eks.cluster_name
}

output "k8tre_eks_access_role" {
  description = "K8TRE EKS deployment role ARN"
  value       = module.k8tre-eks.eks_access_role
}
